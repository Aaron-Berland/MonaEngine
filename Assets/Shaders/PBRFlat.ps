#version 450 core 
layout (location = 3) uniform vec3 albedo;
layout (location = 6) uniform float metallic;
layout (location = 7) uniform float roughness;
layout (location = 8) uniform float ambientOcclusion;
layout (location = 9) uniform vec3 cameraPosition;

out vec4 color;

in vec3 worldPos;
in vec3 normal;

struct DirectionalLight {
	vec3 colorIntensity;
	vec3 direction;
};

struct PointLight {
	vec3 colorIntensity;
	vec3 position;
	float maxRadius;
};

struct SpotLight {
	vec3 colorIntensity; 
	float maxRadius;
	vec3 position; 
	float cosPenumbraAngle;
	vec3 direction;
	float cosUmbraAngle;
};

layout(std140, binding = 0) uniform Lights {
	SpotLight[${MAX_SPOT_LIGHTS}] spotLights;
	PointLight[${MAX_POINT_LIGHTS}] pointLights;
	DirectionalLight[${MAX_DIRECTIONAL_LIGHTS}] directionalLights;
	vec3 ambientLight;
	int spotLightsCount;
	int pointLightsCount;
	int directionalLightsCount;
};


const float PI = 3.14159265359;
float DistributionGGX(vec3 N, vec3 H, float roughness)
{
    float a = roughness*roughness;
    float a2 = a*a;
    float NdotH = max(dot(N, H), 0.0);
    float NdotH2 = NdotH*NdotH;

    float nom   = a2;
    float denom = (NdotH2 * (a2 - 1.0) + 1.0);
    denom = PI * denom * denom;

    return nom / denom;
}

float GeometrySchlickGGX(float NdotV, float roughness)
{
    float r = (roughness + 1.0);
    float k = (r*r) / 8.0;

    float nom   = NdotV;
    float denom = NdotV * (1.0 - k) + k;

    return nom / denom;
}

float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness)
{
    float NdotV = max(dot(N, V), 0.0);
    float NdotL = max(dot(N, L), 0.0);
    float ggx2 = GeometrySchlickGGX(NdotV, roughness);
    float ggx1 = GeometrySchlickGGX(NdotL, roughness);

    return ggx1 * ggx2;
}

vec3 fresnelSchlick(float cosTheta, vec3 F0)
{
    return F0 + (1.0 - F0) * pow(1.0 - min(cosTheta,1.0), 5.0);
}

float GetDistanceAttenuation(vec3 lightVector, float lightRadius)
{
	float squareDistance = dot(lightVector, lightVector);
	float squareRadius = lightRadius * lightRadius;
	float windowing = pow(max(1.0 - pow(squareDistance/squareRadius,2.0f),0.0f),2.0f);
	float distanceAttenuation = windowing * (1 / (squareDistance + 1));
	return distanceAttenuation;
}

float GetAngularAttenuation(vec3 normalizedLightVector, vec3 lightDirection, float lightCosUmbraAngle, float lightCosPenumbraAngle)
{
	float cosSurfaceAngle = dot(lightDirection, normalizedLightVector);
	float t = clamp((cosSurfaceAngle - lightCosUmbraAngle) / (lightCosPenumbraAngle - lightCosUmbraAngle), 0.0f, 1.0f);
	float angularAttenuation = t*t;
	return angularAttenuation;
}

void main()
{
	
	vec3 N = normalize(normal);
	vec3 V = normalize(cameraPosition - worldPos);
	vec3 ambient = ambientLight * albedo * ambientOcclusion;
	vec3 F0 = vec3(0.04); 
    F0 = mix(F0, albedo, metallic);
	vec3 Lo = vec3(0.0f,0.0f,0.0f);
	
	for(int i = 0; i < directionalLightsCount; i++){
		vec3 L = -directionalLights[i].direction;
		vec3 H = normalize(V + L);
		vec3 radiance = directionalLights[i].colorIntensity;
		 // Cook-Torrance BRDF
        float NDF = DistributionGGX(N, H, roughness);   
        float G   = GeometrySmith(N, V, L, roughness);      
        vec3 F    = fresnelSchlick(max(dot(H, V), 0.0), F0);
           
        vec3 nominator    = NDF * G * F; 
        float denominator = 4 * max(dot(N, V), 0.0) * max(dot(N, L), 0.0) + 0.001; // 0.001 to prevent divide by zero.
        vec3 specular = nominator / denominator;
        
        
        vec3 kS = F;
        vec3 kD = vec3(1.0) - kS;
        kD *= 1.0 - metallic;	  

        float NdotL = max(dot(N, L), 0.0);
		Lo += (kD * albedo / PI + specular) * radiance * NdotL;
	}
	
	for(int i = 0; i < pointLightsCount; i++){
		vec3 lightVector = worldPos - pointLights[i].position;
		vec3 L = normalize(pointLights[i].position - worldPos);
		vec3 H = normalize(V + L);
		float distanceAttenuation = GetDistanceAttenuation(lightVector, spotLights[i].maxRadius);
		vec3 radiance  = distanceAttenuation * pointLights[i].colorIntensity;
		
		float NDF = DistributionGGX(N, H, roughness);   
        float G   = GeometrySmith(N, V, L, roughness);      
        vec3 F    = fresnelSchlick(max(dot(H, V), 0.0), F0);
           
        vec3 nominator    = NDF * G * F; 
        float denominator = 4 * max(dot(N, V), 0.0) * max(dot(N, L), 0.0) + 0.001; // 0.001 to prevent divide by zero.
        vec3 specular = nominator / denominator;
        
        
        vec3 kS = F;
        vec3 kD = vec3(1.0) - kS;
        kD *= 1.0 - metallic;	  

        float NdotL = max(dot(N, L), 0.0);
		Lo += (kD * albedo / PI + specular) * radiance * NdotL;
	}

	for(int i = 0; i < spotLightsCount; i++){
		vec3 lightVector = worldPos - spotLights[i].position;
		vec3 L = normalize(spotLights[i].position - worldPos);
		vec3 H = normalize(V + L);

		float distanceAttenuation = GetDistanceAttenuation(lightVector, spotLights[i].maxRadius);
		float angularAttenuation = GetAngularAttenuation(-L, spotLights[i].direction, spotLights[i].cosUmbraAngle, spotLights[i].cosPenumbraAngle);
		vec3 radiance = distanceAttenuation*angularAttenuation*spotLights[i].colorIntensity;
		
		float NDF = DistributionGGX(N, H, roughness);   
        float G   = GeometrySmith(N, V, L, roughness);      
        vec3 F    = fresnelSchlick(max(dot(H, V), 0.0), F0);
           
        vec3 nominator    = NDF * G * F; 
        float denominator = 4 * max(dot(N, V), 0.0) * max(dot(N, L), 0.0) + 0.001; // 0.001 to prevent divide by zero.
        vec3 specular = nominator / denominator;
        
        
        vec3 kS = F;
        vec3 kD = vec3(1.0) - kS;
        kD *= 1.0 - metallic;	  

        float NdotL = max(dot(N, L), 0.0);
		Lo += (kD * albedo / PI + specular) * radiance * NdotL;

	}
	

	vec3 finalColor = ambient + Lo;
	finalColor = finalColor/ (finalColor + vec3(1.0));
	finalColor = pow(finalColor, vec3(1.0/2.2));
	color = vec4(finalColor, 1.0);
}